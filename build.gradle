import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    //id 'me.bristermitten.pdm' version '0.0.26'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'xyz.oribuin'
version '2.0.0'
compileJava.options.encoding = 'UTF-8'

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8


repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    maven { url 'https://repo.rosewooddev.io/repository/public/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.0"

    // Spigot Stuff
    shadow 'org.spigotmc:spigot:1.16.2'
    shadow 'me.clip:placeholderapi:2.10.9'

    implementation 'dev.rosewood:guiframework:1.1.1'
    implementation 'com.github.WesJD.AnvilGUI:anvilgui:master-SNAPSHOT'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// Temporary replacement for PDM
shadowJar {
    mergeServiceFiles()
    archiveFileName = 'FlightTrails-' + project.property("version") + '.jar'

    // Relocate so much shit
    relocate ('org.jetbrains', 'xyz.oribuin.flighttrails.libs.jetbrains')
    relocate ('org.intellij', 'xyz.oribuin.flighttrails.libs.intellij')
    relocate ('kotlin', 'xyz.oribuin.flighttrails.libs.kotlin')
    relocate ('dev.rosewood.guiframework', 'xyz.oribuin.flighttrails.libs.guiframework')
}

//jar.dependsOn project.tasks.getByName('pdm')
jar.dependsOn project.tasks.getByName('shadowJar')